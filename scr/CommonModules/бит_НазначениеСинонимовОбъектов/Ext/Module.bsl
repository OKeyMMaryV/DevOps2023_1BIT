////////////////////////////////////////////////////////////////////////////////
// 1С:Бухучет и торговля
//  ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С МЕХАНИЗМОМ НАЗНАЧЕНИЯ СИНОНИМОВ ОБЪЕКТОВ
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ПолучениеСинонимаОбъекта
	
// Процедура выводит заголовок формы для объекта
// установленный в регистре сведений "бит_СинонимыОбъектов".
// 
// Параметры:
//  МетаданныеОбъекта - МетаданныеОбъекта, СправочникСсылка.бит_ОбъектыСистемы.
//  ФормаОбъекта      - ФормаОбъекта, для которой необходимо установить заголовок.
//  ВидФормы          - ПеречислениеСсылка.бит_ВидыФормОбъекта.
//  ЭтотОбъект        - Объект, по умолчанию Неопределено.
// 
Процедура ВывестиЗаголовокФормы(МетаданныеОбъекта, ФормаОбъекта, ВидФормы, ЭтотОбъект = Неопределено) Экспорт
	
	// Если передана ссылка на объект доступа, тогда.
	Если ТипЗнч(МетаданныеОбъекта) = Тип("СправочникСсылка.бит_ОбъектыСистемы") Тогда
		ОбъектДоступа = МетаданныеОбъекта;
	Иначе
		// Получим объект доступа по метаданным объекта.
		ОбъектДоступа = бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(МетаданныеОбъекта);
	КонецЕсли;
	
    Если Не ЗначениеЗаполнено(ОбъектДоступа) ТОгда
        Возврат;
    КонецЕсли;
    
	Если ВидФормы = Перечисления.бит_ВидыФормОбъекта.Списка Тогда
		
		СинонимОбъекта = ОбъектДоступа.Наименование;
		
	ИначеЕсли ВидФормы = Перечисления.бит_ВидыФормОбъекта.Выбора Тогда
		
		СинонимОбъекта = ОбъектДоступа.ЗаголовокФормыВыбора;
		
	ИначеЕсли ВидФормы = Перечисления.бит_ВидыФормОбъекта.Элемента Тогда
		
		СинонимОбъекта = ОбъектДоступа.ЗаголовокЭлемента;
		
	ИначеЕсли ВидФормы = Перечисления.бит_ВидыФормОбъекта.Документа Тогда
		
		СинонимОбъекта = ОбъектДоступа.ЗаголовокЭлемента;
		
	КонецЕсли; 
	
	ШаблонЗаполнения = "";
	Если Найти(СинонимОбъекта,"[")> 0 И Найти(СинонимОбъекта,"]")>0 Тогда
	
		 ШаблонЗаполнения = СинонимОбъекта;
		 СинонимОбъекта = "";
	
	КонецЕсли; 
       		
	// Если задан синоним, то пользователю необязательно знать техногенное имя.
	Если ТипЗнч(ФормаОбъекта) = Тип("УправляемаяФорма")
		И (ЗначениеЗаполнено(СинонимОбъекта)
		ИЛИ ЗначениеЗаполнено(ШаблонЗаполнения)) Тогда
		
		ФормаОбъекта.АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	// Если шаблон заполнения не установлен, тогда
	// заголовком формы будет являтся установленный синоним.
	Если Не ЗначениеЗаполнено(ШаблонЗаполнения) Тогда
		ФормаОбъекта.Заголовок = ?(ЗначениеЗаполнено(СинонимОбъекта), СинонимОбъекта, ФормаОбъекта.Заголовок);
		Возврат;
	КонецЕсли;
	
	// Установим заголовок формы по шаблону заполнения.
	УстановитьЗаголовокПоШаблонуЗаполнения(МетаданныеОбъекта, ФормаОбъекта, СинонимОбъекта, ШаблонЗаполнения, ЭтотОбъект);

КонецПроцедуры // ВывестиЗаголовокФормы()

#КонецОбласти

#Область ЗаполнениеЗначениямиПоУмолчанию

// Процедура заполняет Синонимы объектов значениями по умолчанию.
// 
Процедура ЗаполнитьСинонимыОбъектовИзМакета() Экспорт
	
	Макет = Справочники.бит_ОбъектыСистемы.ПолучитьМакет("СинонимыПоУмолчанию");
	
    ТаблицаСДанными = Новый ТаблицаЗначений;
    ТаблицаСДанными.Колонки.Добавить("ИмяКоллекции");
    ТаблицаСДанными.Колонки.Добавить("ИмяОбъектаКоллекции");
    ТаблицаСДанными.Колонки.Добавить("ВидФормы");
    ТаблицаСДанными.Колонки.Добавить("СинонимФормыСписка");
    ТаблицаСДанными.Колонки.Добавить("СинонимФормыВыбора");
    ТаблицаСДанными.Колонки.Добавить("СинонимФормыЭлемента");
	
    
    ПоляСтруктуры = "ИмяКоллекции, ИмяОбъектаКоллекции, ВидФормы, СинонимФормыСписка, СинонимФормыВыбора, СинонимФормыЭлемента"; 
    
    НомСтроки = 2;
    Дальше    = Истина;
	
	ЕстьОбъектыМСФО = бит_ОбщегоНазначения.ЕстьОбъектыМСФО();
	
    // Считываем данные из макета.
    Пока Дальше Цикл
        
        Данные    = Новый Структура(ПоляСтруктуры);
        НомКолонки = 1;
        
        Для каждого КлючИЗначение Из Данные Цикл
            
            ИмяПоля  = КлючИЗначение.Ключ;
            Значение = СокрЛП(Макет.Область(НомСтроки, НомКолонки, НомСтроки, НомКолонки).Текст);
            
            Данные[ИмяПоля] = Значение;
            
            НомКолонки = НомКолонки + 1;
            
        КонецЦикла;
        
        НовСтрока = ТаблицаСДанными.Добавить();
        Если ЕстьОбъектыМСФО Или Найти(Данные.Синоним, "международ") = 0 Тогда
		 	ЗаполнитьЗначенияСвойств(НовСтрока, Данные);
		Иначе	
		    ЗаполнитьЗначенияСвойств(НовСтрока, Данные, , "Синоним");
		КонецЕсли;
        
        НомСтроки = НомСтроки + 1;
        
        ТестовоеЗначение = СокрЛП(Макет.Область(НомСтроки, 2, НомСтроки, 2).Текст);
    	Дальше = ЗначениеЗаполнено(ТестовоеЗначение);
        
    КонецЦикла;
    
    Для Каждого ТекСтрока Из ТаблицаСДанными Цикл
        
        // Получим объект доступа по метаданным объекта.
        ОбъектДоступа = бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(Метаданные[ТекСтрока.ИмяКоллекции][ТекСтрока.ИмяОбъектаКоллекции]);
        
        Если Не ЗначениеЗаполнено(ОбъектДоступа) ТОгда
            Продолжить;
        КонецЕсли;
        
        ВидФормы = Перечисления.бит_ВидыФормОбъекта[ТекСтрока.ВидФормы];
        
		Если ЗначениеЗаполнено(ТекСтрока.СинонимФормыСписка) 
			 ИЛИ ЗначениеЗаполнено(ТекСтрока.СинонимФормыВыбора) 
			 ИЛИ ЗначениеЗаполнено(ТекСтрока.СинонимФормыЭлемента) Тогда
		
			ТекОб = ОбъектДоступа.ПолучитьОбъект();
			ТекОб.Наименование         = ТекСтрока.СинонимФормыСписка;
			ТекОб.ЗаголовокФормыВыбора = ТекСтрока.СинонимФормыВыбора;
			ТекОб.ЗаголовокЭлемента    = ТекСтрока.СинонимФормыЭлемента;
			
			ТекОб.ОбменДанными.Загрузка = Истина;
			ТекОб.Записать();
			
		КонецЕсли; 
		
    КонецЦикла;
	
	ТекстСообщения = НСтр("ru='Загрузка синонимов объектов завершена.'");
	бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
	
КонецПроцедуры // ЗаполнитьСинонимыОбъектовИзМакета()

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает заголовок по шаблону заполнения.
// 
// Параметры
//  МетаданныеОбъекта - МетаданныеОбъекта.
//  Элемент           - Форма или ЭлементУправления, для кот. необходимо установить заголовок.
//  СинонимЭлемента   - Строка.
//  ШаблонЗаполнения  - Строка.
//  ЭтотОбъект        - Объект.
// 
Процедура УстановитьЗаголовокПоШаблонуЗаполнения(МетаданныеОбъекта, Элемент, СинонимЭлемента, ШаблонЗаполнения, ЭтотОбъект)

	Попытка
        
        ЗаголовокПоШаблону = "";
        
        // Получим текст заголовка по шаблону.
    	ЗаголовокПоШаблону = ПолучитьТекстЗаголовкаПоШаблону(МетаданныеОбъекта, СинонимЭлемента, ШаблонЗаполнения, ЭтотОбъект);
        Элемент.Заголовок  = ЗаголовокПоШаблону;
        
    Исключение
		ТекстСообщения = НСтр("ru='Не удалось установить заголовок!
		|Ошибка заполнения по шаблону для формы объекта (регистр сведений ""Синонимы объектов"").'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
    КонецПопытки;
    
КонецПроцедуры // УстановитьЗаголовокПоШаблонуЗаполнения()

// Функция получает текст заголовка по шаблону.
// 
// Параметры:
//  МетаданныеОбъекта - МетаданныеОбъекта.
//  СинонимЭлемента   - Строка.
//  ШаблонЗаполнения  - Строка.
//  ЭтотОбъект        - Объект.
// 
// Возвращаемое значение:
//  ЗаголовокПоШаблону - Строка.
// 
Функция ПолучитьТекстЗаголовкаПоШаблону(МетаданныеОбъекта, СинонимЭлемента, Знач ШаблонЗаполнения, ЭтотОбъект)
    
    ЗаголовокПоШаблону = ШаблонЗаполнения;
    
    Поз_1 = Найти(ЗаголовокПоШаблону, "[");
    
    Если Поз_1 <> 0 Тогда
        
        Поз_2           = Найти(ЗаголовокПоШаблону, "]");
        ДляЗамены       = Сред(ЗаголовокПоШаблону, Поз_1, Поз_2 - Поз_1 + 1);
        СвойствоОбъекта = Сред(ЗаголовокПоШаблону, Поз_1 + 1, Поз_2 - Поз_1 - 1);
        
        Если ВРег(СвойствоОбъекта) = "СИНОНИМ" Тогда
            
            ТекстСинонима  = ?(ЗначениеЗаполнено(СинонимЭлемента), СинонимЭлемента, МетаданныеОбъекта.Синоним);
            ЗаголовокПоШаблону = СтрЗаменить(ЗаголовокПоШаблону, ДляЗамены, ТекстСинонима);
            
        Иначе
            ЗаголовокПоШаблону = СтрЗаменить(ЗаголовокПоШаблону, ДляЗамены, ЭтотОбъект[СвойствоОбъекта]);
        КонецЕсли;
        
        Возврат ПолучитьТекстЗаголовкаПоШаблону(МетаданныеОбъекта, СинонимЭлемента, ЗаголовокПоШаблону, ЭтотОбъект);
        
    Иначе
        Возврат ЗаголовокПоШаблону;
    КонецЕсли;
    
КонецФункции // ПолучитьТекстЗаголовкаПоШаблону()

#КонецОбласти
