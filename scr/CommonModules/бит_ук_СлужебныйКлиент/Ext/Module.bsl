#Область ПрограммныйИнтерфейс

// Функция - Получить системную информацию клиент
// 
// Возвращаемое значение:
//  Структура - Структура - параметры системы:
//  * ВидКлиента				- Строка - UID.
//  * ИнформацияЭкрановКлиента	- Строка - Информация о экранах клиента.
//  * СведенияОПоставке			- Строка - Сведения о поставке продукта.
//  * СистемнаяИнформация		- Строка - Информация о системе.
//
Функция ПолучитьСистемнуюИнформациюКлиент() Экспорт 

	СтруктураИнформация = Новый Структура("ВидКлиента, ИнформацияЭкрановКлиента, СведенияОПоставке, СистемнаяИнформация");
	
	# Если ВебКлиент Тогда 
		
		ПолучитьСистемнуюИнформацию(СтруктураИнформация);
		СтруктураИнформация.ВидКлиента = "5C762DA9-AA0B-428A-9242-E63C45B96270";
			
	# КонецЕсли
	
	# Если Клиент Тогда 

		ПолучитьСистемнуюИнформацию(СтруктураИнформация);
		
		# Если ТонкийКлиент Тогда 
			
			СтруктураИнформация.ВидКлиента = "EFFA9A24-A749-48DB-977E-730BF3DB5DB4";
			
		# КонецЕсли	
		
		# Если ТолстыйКлиентУправляемоеПриложение Тогда 
			
			СтруктураИнформация.ВидКлиента = "3DF45137-15F0-449F-A2BF-A1D26008E597";
			
		# КонецЕсли	
		
		# Если ТолстыйКлиентОбычноеПриложение Тогда 
			
			СтруктураИнформация.ВидКлиента = "1C4E2FBF-16C3-4B44-8ADB-D4CEA878C892";
			
		# КонецЕсли 
		
	# КонецЕсли	

	Возврат СтруктураИнформация;
	
КонецФункции

// Процедура получает системную информацию.
//
// Параметры:
//  СтруктураИнформация	 - Структура - параметры системы:
//  * ВидКлиента				- Строка - UID.
//  * ИнформацияЭкрановКлиента	- Строка.
//  * СведенияОПоставке			- Строка.
//  * СистемнаяИнформация		- Строка.
//
Процедура ПолучитьСистемнуюИнформацию(СтруктураИнформация) Экспорт 

	Система = Новый СистемнаяИнформация;
	
	АппаратныеДанные = Новый Структура;
	
	АппаратныеДанные.Вставить("ВерсияОС"						, Система.ВерсияОС);
	АппаратныеДанные.Вставить("ВерсияПриложения"				, Система.ВерсияПриложения);
	АппаратныеДанные.Вставить("ИдентификаторКлиента"			, Строка(Система.ИдентификаторКлиента));
	АппаратныеДанные.Вставить("ИнформацияПрограммыПросмотра"	, Система.ИнформацияПрограммыПросмотра);
	АппаратныеДанные.Вставить("ОперативнаяПамять"				, Система.ОперативнаяПамять);
	АппаратныеДанные.Вставить("Процессор"						, Система.Процессор);
	АппаратныеДанные.Вставить("ТипПлатформы"					, Строка(Система.ТипПлатформы));
	
	СтруктураИнформация.СистемнаяИнформация = АппаратныеДанные;
	
	ИнформацияМонитор = ПолучитьИнформациюЭкрановКлиента();
	РазрешениеМонитора = Новый Массив;
	
	Для каждого СтрИнфо Из ИнформацияМонитор Цикл
		
		СтрРазрешение = Новый Структура("DPI, Высота, Ширина");
		СтрРазрешение.DPI	 = СтрИнфо.DPI;
		СтрРазрешение.Высота = СтрИнфо.Высота;
		СтрРазрешение.Ширина = СтрИнфо.Ширина;
		РазрешениеМонитора.Добавить(СтрРазрешение);
		
	КонецЦикла; 
	
	СтруктураИнформация.ИнформацияЭкрановКлиента = РазрешениеМонитора;

КонецПроцедуры
	
#КонецОбласти 