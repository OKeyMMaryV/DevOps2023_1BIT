////////////////////////////////////////////////////////////////////////////////
// Модуль содержит процедуры подключения к внешним базам. 
// Подключения кешируются механизмом повторного использования возвращаемых значений.
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Функция выполняет подключение к внешней базе посредством ComConnector.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.бит_НастройкиПодключений.
// 
// Возвращаемое значение:
//  Коннектор - ComObject.
// 
Функция ПодключитьКОМ(НастройкаПодключения) Экспорт

	Коннектор = Неопределено;
	
	#Если Сервер ИЛИ ТолстыйКлиентУправляемоеПриложение ИЛИ ТолстыйКлиентОбычноеПриложение Тогда
		
		Коннектор = Справочники.бит_мпд_НастройкиВнешнихПодключений.ПодключитьКОМ(НастройкаПодключения);
		
	#КонецЕсли

	Возврат Коннектор;
	
КонецФункции // ПодключитьКОМ()

// Функция выполняет подключение к внешней базе посредством OLE.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.бит_НастройкиПодключений.
// 
// Возвращаемое значение:
//  Коннектор - ComObject.
// 
Функция ПодключитьОЛЕ(НастройкаПодключения) Экспорт

	Коннектор = Неопределено;
	
	#Если ТолстыйКлиентУправляемоеПриложение ИЛИ ТолстыйКлиентОбычноеПриложение Тогда
		
		Коннектор = Справочники.бит_мпд_НастройкиВнешнихПодключений.ПодключитьОЛЕ(НастройкаПодключения);
		
	#КонецЕсли

	Возврат Коннектор;
	
КонецФункции // ПодключитьОЛЕ()

// Функция получает структуру метаданных целевой внешней базы по HTTP.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.бит_мпд_НастройкиВнешнихПодключений.
// 
// Возвращаемое значение:
//  ОтветHTTP - HTTPОтвет.
// 
Функция ПолучитьМетаданныеHTTP(НастройкаПодключения, Развернуто = Истина, ВыводитьСообщения = Ложь) Экспорт
	
	ПризнакОбращения = бит_мпд_КлиентСервер.ПризнакОбращенияОДата();
	ТекстЗапросаМета =  ПризнакОбращения+?(Развернуто, "$metadata", "");
	
	Путь = НастройкаПодключения.ИмяСервера+"/"+НастройкаПодключения.ИмяБазы;
	
	СоединениеHTTP = Новый HTTPСоединение(Путь,,НастройкаПодключения.Пользователь,НастройкаПодключения.Пароль);
	
	ЗапросHTTP = Новый HTTPЗапрос(ТекстЗапросаМета);
	
	ОтветHTTP = СоединениеHTTP.Получить(ЗапросHTTP);
	
	Если НЕ ОтветHTTP.КодСостояния = 200 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось получить метаданные по настройке ""%1"". 
										|Код ошибки: %2.
										|Подробности см. на: http://www.w3.org/Protocols/rfc2616/rfc2616-sec10.html'"),
							НастройкаПодключения, ОтветHTTP.КодСостояния);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли; 
	
	Возврат ОтветHTTP;
	
КонецФункции // ПолучитьМетаданныеHTTP()

#КонецОбласти
