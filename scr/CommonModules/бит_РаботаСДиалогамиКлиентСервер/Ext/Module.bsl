
#Область СлужебныйПрограммныйИнтерфейс

#Область СозданиеЭлементовУправленияБюджетирование

// Создание элементов управления для реквизитов табличных частей.
// 
// Параметры:
//  ЭлементыФормы - ВсеЭлементыФормы.
//  ДокументОбъект - ДокументОбъект.
// 
Процедура СоздатьЭлементыУправленияДляТЧ(ДокументОбъект, ЭлементыФормы) Экспорт
	
	// Устарело.
	// Управление формой реализовано в переопределяемом модуле бит_РаботаСДиалогамиПереопределяемый.
	
КонецПроцедуры // СоздатьЭлементыУправленияДляТЧ()

#КонецОбласти
	
// Формирует и устанавливает текст заголовка формы документа.
// 
// Параметры:
//  ФормаДокумента 	   - УправляемаяФорма, ФормаОбычная.
//  СтруктураЗаголовка - Структура:
// 							ЭтоНовый 			 - Булево
// 							ПредставлениеОбъекта - Строка
// 							СтрокаВидаОперации 	 - Строка (По умолчанию = "")
// 							ДокументПроведен 	 - Булево (По умолчанию = Ложь)
// 							ПроведениеРазрешено  - Булево (По умолчанию = Истина).
// 
Процедура УстановитьЗаголовокФормыДокумента(ФормаДокумента, СтруктураЗаголовка) Экспорт

	ФормаДокумента.АвтоЗаголовок = Ложь; // Заголовок будем писать сами.
	
	// Получим обязательные параметры.
	флЭтоНовый		   	   = СтруктураЗаголовка.ЭтоНовый;
	ПредставлениеДокумента = СтруктураЗаголовка.ПредставлениеОбъекта;
	Если ПредставлениеДокумента = Неопределено Тогда
		ПредставлениеДокумента = "";	
	КонецЕсли;
	
	// Получим необязательные параметры.
	СтрокаВидаОперации = ?(СтруктураЗаголовка.Свойство("СтрокаВидаОперации"), СтруктураЗаголовка.СтрокаВидаОперации, "");
	
	ДокументПроведен = ?(СтруктураЗаголовка.Свойство("ДокументПроведен"), СтруктураЗаголовка.ДокументПроведен, Ложь);
	ПроведениеРазрешено = ?(СтруктураЗаголовка.Свойство("ПроведениеРазрешено"), СтруктураЗаголовка.ПроведениеРазрешено, Истина);
		
	// Получим заголовок формы документа.
	Если ПустаяСтрока(СтрокаВидаОперации) Тогда
		Заголовок = ПредставлениеДокумента + ": ";
	Иначе
		Заголовок = ПредставлениеДокумента + ": " + СтрокаВидаОперации + ". ";
	КонецЕсли;
		
	Если флЭтоНовый Тогда  
		Заголовок = Заголовок +  НСтр("ru = 'Новый'");
	Иначе
		
		Если ДокументПроведен Тогда
			Заголовок = Заголовок +  НСтр("ru = 'Проведен'");
		ИначеЕсли ПроведениеРазрешено Тогда
			Заголовок = Заголовок +  НСтр("ru = 'Не проведен'");
		Иначе
			Заголовок = Заголовок +  НСтр("ru = 'Записан'");
		КонецЕсли;
		
	КонецЕсли;
	
	// Установим полученный заголовок.
	ФормаДокумента.Заголовок = Заголовок;

КонецПроцедуры // УстановитьЗаголовокФормыДокумента()

//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2019-10-15 (#3488)
Процедура ВыполнитьБлокировкуПолейНаФормеОбъекта(Параметры, СписокДоступныхПолей) Экспорт
	
	Форма = Параметры.Форма;
	
	МассивТЧДляРазблокировки = Новый Массив;
	Для каждого ТекущийЭлемент Из Форма.Элементы Цикл
		
		Если ТипЗнч(ТекущийЭлемент) = Тип("ПолеФормы") Тогда
			
			НайденнаяСтрока = СписокДоступныхПолей.НайтиПоЗначению(ТекущийЭлемент.Имя);
			Если НайденнаяСтрока <> Неопределено Тогда
				ТекущийЭлемент.Доступность = Истина;
				ТекущийЭлемент.ТолькоПросмотр = Ложь;
				Если ТипЗнч(ТекущийЭлемент.Родитель) = Тип("ТаблицаФормы") Тогда 
					ТекущийЭлемент.Родитель.Доступность = Истина;
					ТекущийЭлемент.Родитель.ТолькоПросмотр = Ложь;
					ТекущийЭлемент.Родитель.ИзменятьСоставСтрок  = Истина;
					ТекущийЭлемент.Родитель.ИзменятьПорядокСтрок = Истина;
					МассивТЧДляРазблокировки.Добавить(ТекущийЭлемент.Родитель);
				КонецЕсли;
			Иначе												
				ТекущийЭлемент.Доступность = Ложь;
				ТекущийЭлемент.ТолькоПросмотр = Истина;
			КонецЕсли;
			
			Если НЕ ТекущийЭлемент.Доступность Тогда
				ТекущийЭлемент.ЦветТекста = Новый Цвет(149,149,149);
			КонецЕсли; 
			
		ИначеЕсли ТипЗнч(ТекущийЭлемент) = Тип("КнопкаФормы") Тогда								
			
			НайденнаяСтрока = СписокДоступныхПолей.НайтиПоЗначению(ТекущийЭлемент.Имя);
			Если НайденнаяСтрока <> Неопределено Тогда
				ТекущийЭлемент.Доступность = Истина;
			Иначе												
				ТекущийЭлемент.Доступность = Ложь;
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ТекущийЭлемент) = Тип("ТаблицаФормы") 
			И МассивТЧДляРазблокировки.Найти(ТекущийЭлемент) = Неопределено Тогда								
			
			ТекущийЭлемент.Доступность = Ложь;
			ТекущийЭлемент.ТолькоПросмотр = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТипЗнч(Параметры.Объект) = Тип("Строка")
		И НРег(Параметры.Объект) = "документ.бит_заявканарасходованиесредств" 
		ИЛИ ТипЗнч(Параметры.Объект) = Тип("СправочникСсылка.бит_ОбъектыСистемы")
		И НРег(Параметры.Объект.ИмяОбъектаПолное) = "документ.бит_заявканарасходованиесредств" Тогда
		
		Если Форма.Элементы.Найти("Сумма") <> Неопределено Тогда
			
			Если (Форма.Элементы.Сумма.Доступность
				ИЛИ НЕ Форма.Элементы.Сумма.ТолькоПросмотр)
				И НЕ Форма.Объект.Ок_ПересчетКурса Тогда
				
				Форма.Элементы.Сумма.Доступность = Ложь;
				Форма.Элементы.Сумма.ТолькоПросмотр = Ложь;
				Форма.Элементы.Сумма.ЦветТекста = Новый Цвет(149,149,149);
								
			КонецЕсли;
			
		КонецЕсли; 
		
		Если Форма.Элементы.Найти("ПолеСуммаДляОтображенияВРежимеРедактирования") <> Неопределено
			И Форма.Элементы.Найти("РаспределениеИтогСумма") <> Неопределено Тогда
			
			Форма.Элементы.ПолеСуммаДляОтображенияВРежимеРедактирования.Видимость = Истина;
			Форма.Элементы.ПолеСуммаДляОтображенияВРежимеРедактирования.ТолькоПросмотр = Истина;
			Форма.Элементы.РаспределениеИтогСумма.Видимость = Ложь;
			
		КонецЕсли; 
		
		Если Форма.Элементы.Найти("ДокументОснование") <> Неопределено Тогда
			
			Форма.Элементы.ДокументОснование.Доступность = Истина;
			Форма.Элементы.ДокументОснование.ТолькоПросмотр = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2019-10-15 (#3488)

#КонецОбласти
