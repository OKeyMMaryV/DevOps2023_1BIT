// Содержимое модуля отличается в версиях ПРОФ и КОРП

// Расширяет перечень разрезов распределения: дополняет его информацией о субконто без флага "Только обороты",
// добавленных пользователем на счета затрат.
//
// Параметры:
//  РазрезыРаспределения    - ТаблицаЗначений - см. см. НовыйРазрезыРаспределения(), дополняемая коллекция
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - содержит таблицу СчетаРасходов
//
Процедура ДополнитьРазрезыРаспределенияСубконтоПользователя(РазрезыРаспределения, МенеджерВременныхТаблиц) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("РасширенныйФункционал") Тогда
		Возврат;
	КонецЕсли;
	
	ЗарезервированныеИмена = Новый Структура;
	ЗарезервированныеИмена.Вставить("Номенклатура");
	Для Каждого ОписаниеРазрезаРаспределения Из РазрезыРаспределения Цикл
		ЗарезервированныеИмена.Вставить(ОписаниеРазрезаРаспределения.Имя);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПредопределенныеВидыСубконто", РазрезыРаспределения.ВыгрузитьКолонку("ВидСубконто"));
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыСубконто.ВидСубконто КАК ВидСубконто
	|ПОМЕСТИТЬ ВТ_ВидыСубконтоПользователя
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаРасходов КАК СчетаРасходов
	|		ПО ВидыСубконто.Ссылка = СчетаРасходов.Счет
	|ГДЕ
	|	НЕ ВидыСубконто.Предопределенное
	|	И НЕ ВидыСубконто.ТолькоОбороты
	|	И ВидыСубконто.Суммовой
	|	И НЕ ВидыСубконто.ВидСубконто В (&ПредопределенныеВидыСубконто)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыСубконтоПользователя.ВидСубконто КАК ВидСубконто,
	|	ВидыСубконтоПользователя.ВидСубконто.ТипЗначения КАК ТипЗначения,
	|	ВидыСубконтоПользователя.ВидСубконто.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
	|ИЗ
	|	ВТ_ВидыСубконтоПользователя КАК ВидыСубконтоПользователя
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидСубконто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ВидыСубконтоПользователя";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ОписаниеРазрезаРаспределения = РазрезыРаспределения.Добавить();
		
		Имя = Выборка.ИмяПредопределенныхДанных;
		
		Если ПустаяСтрока(Имя) Или ЗарезервированныеИмена.Свойство(Имя) Тогда
			Имя = РаспределениеРасходов.ИмяСубконтоПользователя(Выборка.ВидСубконто);
		КонецЕсли;
		
		ОписаниеРазрезаРаспределения.Имя         = Имя;
		ОписаниеРазрезаРаспределения.ВидСубконто = Выборка.ВидСубконто;
		ОписаниеРазрезаРаспределения.ТипЗначения = Выборка.ТипЗначения;
		
	КонецЦикла;
	
КонецПроцедуры
