////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// +СБ. Михайлов Никита. 2014-04-30. Загрузка платежных документов AXAPTA 
Функция УстановитьСоединение(ОписаниеОшибкиПодключения = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Истина;
	
	ПараметрыСоединения = ВнешниеИсточникиДанных.СБ_ПлатежныеПорученияAXAPTA.ПолучитьОбщиеПараметрыСоединения();
	ДанныеПараметровСоединения = ПолучитьПараметрыСоединения();
	ПараметрыСоединения.АутентификацияСтандартная = Истина;
	ПараметрыСоединения.ИмяПользователя = ДанныеПараметровСоединения.ИмяПользователя;
	ПараметрыСоединения.Пароль = ДанныеПараметровСоединения.Пароль;
	ПараметрыСоединения.СтрокаСоединения = ДанныеПараметровСоединения.СтрокаСоединения;
	ПараметрыСоединения.СУБД = ДанныеПараметровСоединения.СУБД;
	ВнешниеИсточникиДанных.СБ_ПлатежныеПорученияAXAPTA.УстановитьОбщиеПараметрыСоединения(ПараметрыСоединения);
	
	Попытка
		ВнешниеИсточникиДанных.СБ_ПлатежныеПорученияAXAPTA.УстановитьСоединение();		
	Исключение
		
		ОписаниеОшибкиПодключения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат = Ложь;
		
	КонецПопытки; 
	
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2020-09-07 (#3816)
	Если НЕ Результат Тогда
	
		ВнешниеИсточникиДанных.СБ_ПлатежныеПорученияAXAPTA.УстановитьОбщиеПараметрыСоединения(ПараметрыСоединения);
		ВнешниеИсточникиДанных.СБ_ПлатежныеПорученияAXAPTA.УстановитьПараметрыСоединенияСеанса(ПараметрыСоединения);
	
		Попытка
			ВнешниеИсточникиДанных.СБ_ПлатежныеПорученияAXAPTA.УстановитьСоединение();		
			Результат = Истина;
		Исключение
			
			ОписаниеОшибкиПодключения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Результат = Ложь;
			
		КонецПопытки;
	
	КонецЕсли; 
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2020-09-07 (#3816)
	
	Возврат Результат;
	
КонецФункции // -СБ. Михайлов Никита

// +СБ. Михайлов Никита. 2014-04-30. Загрузка платежных документов AXAPTA 
Функция ПолучитьДанныеИсточника(ИдентификаторыЗаписей) Экспорт 
	
	СхемаИсточникаДанных = Обработки.СБ_ЗагрузкаПлатежныхДокументов_AXAPTA.ПолучитьМакет("СведенияИсточникаПоИдентификатору");
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаИсточникаДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаИсточникаДанных.НастройкиПоУмолчанию);
	СБ_ОбщегоНазначенияКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "МассивИдентификаторов", ИдентификаторыЗаписей);

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаИсточникаДанных, 
												  КомпоновщикНастроек.Настройки,
												  ,
												  , 
												  Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"), 
												  Ложь);

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,,, Истина);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	Возврат ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецФункции // -СБ. Михайлов Никита

// +СБ. Михайлов Никита. 2014-04-05. Загрузка платежных документов AXAPTA 
// Создает и возвращает объект ADODB.Connection для прямого соединения к базе-источнику сведений.
// средствами СУБД. Используется в связи невозможностью применить возможности платформы 8.3 в текущий момент.
// При использовании следует обернуть в попытку-исключение для диагностирования ошибок.
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//	COMОбъект - в случае установки соединения возращается ADODB.Connection
//
Функция СоединениеКИсточникуСредствамиSQL() Экспорт
	
	ПараметрыСоединения = СБ_ЗагрузкаПД_AXAPTA_УправлениеИсточником.ПолучитьПараметрыСоединения();
	
	СтрокаСоединения = "driver={SQL Server};server="
					 + ПараметрыСоединения.Сервер
					 + ";uid="
					 + ПараметрыСоединения.ИмяПользователя
					 + ";pwd="
					 + ПараметрыСоединения.Пароль
					 + ";Database="
					 + ПараметрыСоединения.База; 
					 
	Соединение = Новый COMОбъект("ADODB.Connection"); 
	Соединение.Open(СтрокаСоединения); 
		
	Возврат Соединение;
	
КонецФункции // -СБ. Михайлов Никита

// +СБ. Михайлов Никита. 2014-04-05. Загрузка платежных документов AXAPTA 
// Создает и возвращает объект ADODB.Command для выполнения запроса к базе-источнику сведений.
// При использовании следует обернуть в попытку-исключение для диагностирования ошибок.
//
// Параметры:
//  Соединение - COMОбъект - COMОбъект соединение с базой данных методом СоединениеКИсточникуСредствамиSQL
//
// Возвращаемое значение:
//  COMОбъект - Объект запроса к базе данных
//
Функция ОбъектЗапросSQL(Соединение) Экспорт 
	
	ОбъектЗапрос = Новый COMОбъект("ADODB.Command");
	ОбъектЗапрос.ActiveConnection = Соединение; 

	Возврат ОбъектЗапрос;
	
КонецФункции // -СБ. Михайлов Никита

// +СБ. Михайлов Никита. 2014-05-14. Загрузка платежных документов AXAPTA   
Функция ПолучитьПараметрыСоединения() Экспорт 
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИмяПользователя");
	Результат.Вставить("Пароль");
	Результат.Вставить("Сервер");
	Результат.Вставить("База");
	Результат.Вставить("СтрокаСоединения");
	Результат.Вставить("СУБД", "MSSQLServer");
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	бит_ок_НастройкиМеханизмаИмпортаДанных.ИмяНастройки,
	|	бит_ок_НастройкиМеханизмаИмпортаДанных.Значение
	|ИЗ
	|	РегистрСведений.бит_ок_НастройкиМеханизмаИмпортаДанных КАК бит_ок_НастройкиМеханизмаИмпортаДанных
	|ГДЕ
	|	бит_ок_НастройкиМеханизмаИмпортаДанных.Группа = &Группа";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Группа", ИмяГруппыХраненияНастроек());
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ИмяНастройки = "ИмяПользователя" Тогда
			Результат.ИмяПользователя = Выборка.Значение;	
		ИначеЕсли Выборка.ИмяНастройки = "Пароль" Тогда
			Результат.Пароль = Выборка.Значение;	
		ИначеЕсли Выборка.ИмяНастройки = "Сервер" Тогда
			Результат.Сервер = Выборка.Значение;	
		ИначеЕсли Выборка.ИмяНастройки = "База" Тогда
			Результат.База = Выборка.Значение;	
		ИначеЕсли Выборка.ИмяНастройки = "СтрокаСоединения" Тогда
			Результат.СтрокаСоединения = Выборка.Значение;	
		КонецЕсли;
		
	КонецЦикла; 
	
	Возврат Результат;
	
КонецФункции // -СБ. Михайлов Никита

// +СБ. Михайлов Никита. 2014-05-14. Загрузка платежных документов AXAPTA   
Функция ИмяГруппыХраненияНастроек() Экспорт 
	
	Возврат "ПараметрыИмпортаПлатежныхДокументовAXAPTA";	
	
КонецФункции // -СБ. Михайлов Никита



////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
