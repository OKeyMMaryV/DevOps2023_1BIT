
#Область РаботаСФайлами
	
// Процедура выполняет сохранение файла на диск.
// 
// Параметры:
//  ФайлСсылка - СправочникСсылка.бит_ХранилищеДополнительнойИнформации.
//  ИмяФайла   - Строка.
// 
Процедура СохранитьФайл(ФайлСсылка, Знач ИмяФайла) Экспорт
	
	Если ИмяФайла = "" Тогда		
		ПоказатьПредупреждение( , НСтр("ru = 'Нет сохраненного в базе файла'"));		
		Возврат;
	КонецЕсли;
	
	СсылкаНаФайлВИБ = ПолучитьНавигационнуюСсылку(ФайлСсылка, "Хранилище");
	
	#Если Не ВебКлиент Тогда
		
		ИмяСРасширением = ИмяФайла;
		
		ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ВыборФайла.МножественныйВыбор = Ложь;
		ВыборФайла.ПолноеИмяФайла 	  = ИмяСРасширением;
		ВыборФайла.Фильтр			  = НСтр("ru = 'Все файлы (*.*)|*.*'");
		
		Если ВыборФайла.Выбрать() Тогда
			
			ИмяСРасширением = ВыборФайла.ПолноеИмяФайла;
			РасширениеФайла = бит_ХранениеДополнительнойИнформации.ПолучитьРасширениеФайла(ИмяФайла);
			РасширениеФайла = "." + РасширениеФайла;
			
			Если Найти(ИмяСРасширением, РасширениеФайла) = 0 Тогда
				ИмяСРасширением = ИмяСРасширением + РасширениеФайла;
			КонецЕсли;
			
			ТекстПояснения = НСтр("ru = 'Выполняется сохранение файла """ + ИмяФайла + """ ...
						   		  |Пожалуйста, подождите.'");
						   
			Состояние(ТекстПояснения);	
			
			Файл = Новый файл(ИмяСРасширением);
			Если Файл.Существует() Тогда
				
				Файл.УстановитьТолькоЧтение(Ложь);
				УдалитьФайлы(ИмяСРасширением);
				
			КонецЕсли;
			
			// Сохраним Файл из БД на диск.
			Если ПолучитьФайл(СсылкаНаФайлВИБ, ИмяСРасширением, Ложь) Тогда
				
				// Для варианта с хранением файлов на диске (на сервере) удаляем Файл из временного хранилища после получения.
				Если ЭтоАдресВременногоХранилища(СсылкаНаФайлВИБ) Тогда
					УдалитьИзВременногоХранилища(СсылкаНаФайлВИБ);
				КонецЕсли;
				
				НовыйФайл = Новый Файл(ИмяСРасширением);
				
				Состояние(НСтр("ru = 'Файл успешно сохранен'"), , ИмяСРасширением);
				
			КонецЕсли;
			
		КонецЕсли; // Если ВыборФайла.Выбрать() Тогда
		
	#Иначе
		
		// Сохраним Файл из БД на диск.
		ПолучитьФайл(СсылкаНаФайлВИБ, ИмяФайла, Истина);
		
		// Для варианта с хранением файлов на диске (на сервере) удаляем Файл из временного хранилища после получения.
		Если ЭтоАдресВременногоХранилища(СсылкаНаФайлВИБ) Тогда
			УдалитьИзВременногоХранилища(СсылкаНаФайлВИБ);
		КонецЕсли;
		
	#КонецЕсли
	
КонецПроцедуры // СохранитьФайл()

// Процедура выполняет открытие файла.
// 
// Параметры:
//  ФайлСсылка - СправочникСсылка.бит_ХранилищеДополнительнойИнформации.
//  ИмяФайла   - Строка.
// 
Процедура ОткрытьФайл(ФайлСсылка, Знач ИмяФайла) Экспорт
	
	Если ИмяФайла = "" Тогда   		
		ПоказатьПредупреждение( , НСтр("ru = 'Нет сохраненного в базе файла'"));		
		Возврат;
	КонецЕсли;
	
	#Если Не ВебКлиент Тогда
		
		ИмяОткрываемогоФайла = "";
		
		ИмяКаталога  = ПолучитьИмяКаталога();
		ИмяФайла	 = ПолучитьИмяФайла(ИмяКаталога, ИмяФайла);
		ФайлСохранен = СохранитьВременныйФайл(ФайлСсылка, ИмяФайла);
		
		Если ФайлСохранен Тогда
			ОткрытьФайлПриложением(ИмяФайла);
		КонецЕсли;
		
	#Иначе
	
		Файл = Новый Файл(ИмяФайла);
	
		СсылкаНаФайлВИБ = ПолучитьНавигационнуюСсылку(ФайлСсылка, "Хранилище");
		
		ПолучитьФайл(СсылкаНаФайлВИБ, ИмяФайла, Истина);
		
		// Для варианта с хранением файлов на диске (на сервере) удаляем Файл из временного хранилища после получения.
		Если ЭтоАдресВременногоХранилища(СсылкаНаФайлВИБ) Тогда
			УдалитьИзВременногоХранилища(СсылкаНаФайлВИБ);
		КонецЕсли;
		
	#КонецЕсли
	
КонецПроцедуры // ОткрытьФайл()

// Функция сохраняет файл во временный каталог.
// 
// Параметры:
//  ФайлСсылка - СправочникСсылка.бит_ХранилищеДополнительнойИнформации.
//  ИмяФайла   - Строка.
// 
// Возвращаемое значение:
//  ФайлСохранен - Булево.
// 
Функция СохранитьВременныйФайл(ФайлСсылка, ИмяФайла)
	
	ФайлСохранен = Истина;
	
	Попытка

		ФайлНаДиске    = Новый Файл(ИмяФайла);
		КаталогНаДиске = Новый Файл(ФайлНаДиске.Путь);

		Если Не КаталогНаДиске.Существует() Тогда
			СоздатьКаталог(ФайлНаДиске.Путь);
		КонецЕсли;

		Если ФайлНаДиске.Существует() Тогда
			
			// Если существующему файлу установлено ТолькоЧтение, отменим эту установку.
			Если ФайлНаДиске.ПолучитьТолькоЧтение() Тогда
				ФайлНаДиске.УстановитьТолькоЧтение(Ложь);
			КонецЕсли;

		КонецЕсли;

		СсылкаНаФайлВИБ = ПолучитьНавигационнуюСсылку(ФайлСсылка, "Хранилище");
		
		// Сохраним Файл из БД на диск.
		Если ПолучитьФайл(СсылкаНаФайлВИБ, ИмяФайла, Ложь) Тогда
			
			ФайлНаДиске.УстановитьТолькоЧтение(Ложь);
			
			// Для варианта с хранением файлов на диске (на сервере) удаляем Файл из временного хранилища после получения.
			Если ЭтоАдресВременногоХранилища(СсылкаНаФайлВИБ) Тогда
				УдалитьИзВременногоХранилища(СсылкаНаФайлВИБ);
			КонецЕсли;
			
		КонецЕсли;
		
	Исключение

		ПоказатьПредупреждение( , ОписаниеОшибки());
		ФайлСохранен = Ложь;

	КонецПопытки;

	Возврат ФайлСохранен;
	
КонецФункции // ПолучитьПолноеИмяФайла()

// Функция предназначена для открытия файла соответствующим приложением.
// 
// Параметры:
//  ИмяОткрываемогоФайла - Строка.
// 
Процедура ОткрытьФайлПриложением(ИмяОткрываемогоФайла)
	
	// Открыть Файл.
	Попытка
		
		ЗапуститьПриложение(ИмяОткрываемогоФайла);
		
	Исключение
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение("" + ОписаниеОшибки());
	КонецПопытки;
		
КонецПроцедуры // ОткрытьФайлПриложением()

// Составляет полное имя файла из имени каталога и имени файла.
// 
// Параметры
//  ИмяКаталога  – Строка, содержащая путь к каталогу файла на диске.
//  ИмяФайла     – Строка, содержащая имя файла, без имени каталога.
// 
// Возвращаемое значение:
//   Строка – полное имя файла с учетом каталога.
// 
Функция ПолучитьИмяФайла(ИмяКаталога, ИмяФайла)
	
	Если Найти(ИмяКаталога, "/") > 0 Тогда 
		Слэш = "/";		
	Иначе 
		Слэш = "\";
	КонецЕсли;
	
	Если Не ПустаяСтрока(ИмяФайла) Тогда
		Возврат "" + ИмяКаталога + ?(Прав(ИмяКаталога, 1) = Слэш, "", Слэш) + ИмяФайла;	
	Иначе
		Возврат ИмяКаталога;
	КонецЕсли;

КонецФункции // ПолучитьИмяФайла()

#Если Не ВебКлиент Тогда

// Функция ПолучитьИмяКаталога возвращает полный путь
// к новому каталогу в каталоге временных файлов.
// 
// Параметры:
//  Идентификатор - Строка, начальная часть имени каталога во временном каталоге.
// 
// Возвращаемое значение:
//  Строка - полное имя временного каталога, например "КаталогВременныхФайлов() + Идентификатор\".
// 
Функция ПолучитьИмяКаталога(знач Идентификатор = "kladr_files")
	
	Индекс = 0;
	
	Пока Истина Цикл
		
		ПутьККаталогу  = КаталогВременныхФайлов() + Идентификатор + строка(Индекс) + "\";
		КаталогНаДиске = Новый Файл(ПутьККаталогу);
		
		Если НЕ КаталогНаДиске.Существует() Тогда
			
			СоздатьКаталог(ПутьККаталогу);
			
			Возврат ПутьККаталогу;
		КонецЕсли;
		
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
КонецФункции // ПолучитьИмяКаталога()

#КонецЕсли

#КонецОбласти
