
#Область СлужебныйПрограммныйИнтерфейс

#Область ДополнительныеАналитики

// Процедура осуществляет выбор типа составной аналитики из списка. 
// 
Процедура ВыбратьТипСоставнойАналитики_Завершение(ВыбранныйЭлемент, ДопПараметры) Экспорт

	Контейнер 		     = ДопПараметры.Контейнер;
	ИмяАналитики 	     = ДопПараметры.ИмяАналитики;
    ИмяЭлемента          = ДопПараметры.ИмяЭлемента;
	НастройкиИзмерений   = ДопПараметры.НастройкиИзмерений;
	ЭлементВладелец      = ДопПараметры.ЭлементВладелец;
	ТекущаяНастройка     = ДопПараметры.ТекущаяНастройка;
	СтандартнаяОбработка = ДопПараметры.СтандартнаяОбработка;
	
	ТипКонтейнераСоответствие = ТипЗнч(Контейнер) = Тип("Соответствие");
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		
		// Устанавливаем выбранный тип
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ВыбранныйЭлемент.Значение);
		Описание = Новый ОписаниеТипов(МассивТипов); 					
		Если ТипКонтейнераСоответствие Тогда
		 	Контейнер.Вставить(ИмяЭлемента, Описание.ПривестиЗначение());
		Иначе
			Контейнер[ИмяЭлемента] = Описание.ПривестиЗначение();	
		КонецЕсли;   					
		ЭлементВладелец.ВыбиратьТип     = Ложь;
		ЭлементВладелец.ОграничениеТипа = Описание;
		
		СтандартнаяОбработка = Истина;
		
	Иначе
		
		ЭлементВладелец.ОграничениеТипа = ТекущаяНастройка.ТипЗначения;
		СтандартнаяОбработка = Ложь;
		Возврат;
		
	КонецЕсли;    
	
	// При необходимости открываем форму дополнительных значений аналитики с отбором.
	ОткрытьФормуДопЗначенийАналитик(Контейнер, ИмяАналитики, ИмяЭлемента, НастройкиИзмерений, ЭлементВладелец);  
	
КонецПроцедуры // ВыбратьТипСоставнойАналитики_Завершение()

// Процедура осуществляет выбор типа составной аналитики из списка. Если аналитика типа 
// СправочникСсылка.бит_ДополнительныеЗначенияАналитик то открывается справочник с отбором.
// 
// Параметры:
//  ТекущаяФорма     	 - Форма.
//  ЭлементВладелец  	 - ЭлементУправления.
//  Контейнер        	 - ДанныеФормыКоллекция, ДанныеФормыЭлементКоллекции, Соответствие.
//  ИмяАналитики   	 	 - Строка.
//  СтандартнаяОбработка - Булево.
//  НастройкиИзмерений   - Соответствие.
//  ИмяЭлемента          - Строка (По умлчанию = Неопределено).
// 
Процедура ВыбратьТипСоставнойАналитики(ТекущаяФорма
	                                   , ЭлементВладелец
									   , Контейнер
									   , ИмяАналитики
									   , СтандартнаяОбработка
									   , НастройкиИзмерений
                                       , ИмяЭлемента = Неопределено) Экспорт
                                       
	Если Контейнер = Неопределено Тогда 	   
		Возврат; 	   
    КонецЕсли;
    
    Если ИмяЭлемента = Неопределено Тогда
        ИмяЭлемента = ИмяАналитики;   
    КонецЕсли;

	ТипКонтейнераСоответствие = ТипЗнч(Контейнер) = Тип("Соответствие");
	ТекущееЗначение 		  = ?(ТипКонтейнераСоответствие, Контейнер.Получить(ИмяЭлемента), Контейнер[ИмяЭлемента]);    
	
	Если ТекущееЗначение = Неопределено Тогда
		
		// В колонке Неопределено - значит необходимо выбирать тип.
		ТекущаяНастройка = НастройкиИзмерений[ИмяАналитики];
		Если ТекущаяНастройка <> Неопределено Тогда
			Если ТекущаяНастройка.ЭтоСоставнойТип Тогда
				
				// Формируем список выбора
				МассивТипов = ТекущаяНастройка.ТипЗначения.Типы();
				СписокВыбора = Новый СписокЗначений;
				Для каждого ТекТип Из МассивТипов Цикл 					
					ТекПредставление = Строка(ТекТип);
					СписокВыбора.Добавить(ТекТип,ТекПредставление); 					
				КонецЦикла; 
				
				// Выбираем тип из списка
				СтандартнаяОбработка = Ложь;
				ДопПараметры = Новый Структура;
				ДопПараметры.Вставить("Контейнер"		    , Контейнер);
				ДопПараметры.Вставить("ИмяАналитики"	    , ИмяАналитики);
                ДопПараметры.Вставить("ИмяЭлемента"	        , ИмяЭлемента);
				ДопПараметры.Вставить("НастройкиИзмерений"  , НастройкиИзмерений);
				ДопПараметры.Вставить("ЭлементВладелец"	    , ЭлементВладелец);
				ДопПараметры.Вставить("ТекущаяНастройка"    , ТекущаяНастройка);
				ДопПараметры.Вставить("СтандартнаяОбработка", СтандартнаяОбработка);
				ОбработчикВТ = Новый ОписаниеОповещения("ВыбратьТипСоставнойАналитики_Завершение", бит_МеханизмДопИзмеренийКлиент.ЭтотОбъект, ДопПараметры);
				ТекущаяФорма.ПоказатьВыборИзСписка(ОбработчикВТ, СписокВыбора);// , ЭлементВладелец);
											
			Иначе
				
				// Значение не составное - сразу установим
				Если ТипКонтейнераСоответствие Тогда
				 	ТекущееЗначение = Контейнер.Получить(ИмяЭлемента);
					Контейнер.Вставить(ИмяЭлемента, ТекущаяНастройка.ТипЗначения.ПривестиЗначение(ТекущееЗначение));
				Иначе
					Контейнер[ИмяЭлемента] = ТекущаяНастройка.ТипЗначения.ПривестиЗначение(Контейнер[ИмяЭлемента]);	
				КонецЕсли;
				ЭлементВладелец.ОграничениеТипа = ТекущаяНастройка.ТипЗначения; 
				
			КонецЕсли;
		КонецЕсли; // Нашли настройку измерения
		
	Иначе
		                                   		
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ТипЗнч(ТекущееЗначение));   		
		Описание = Новый ОписаниеТипов(МассивТипов);
		
		ЭлементВладелец.ОграничениеТипа = Описание;		
		ЭлементВладелец.ВыбиратьТип     = Ложь;
		
	КонецЕсли; // Текущее значение неопределено

	// При необходимости открываем форму дополнительных значений аналитики с отбором.
	ОткрытьФормуДопЗначенийАналитик(Контейнер, ИмяАналитики, ИмяЭлемента, НастройкиИзмерений, ЭлементВладелец);  
	
КонецПроцедуры // ВыбратьТипСоставнойАналитики()

#КонецОбласти

#Область ДополнительныеИзмерения

// Процедура обрабатывает событие очистки дополнительного измерения.
// 
// Параметры:
//  Контейнер - СтрокаТабличнойЧасти,ДокументОбъект.
//  ИмяПоля   - Строка.
//  СтандартнаяОбработка - Ложь.
//  НастройкиИзмерений - Соответствие.
// 
Процедура ОбработкаОчисткиДополнительногоИзмерения(ЭлементВладелец, Контейнер, ИмяПоля, СтандартнаяОбработка, НастройкиИзмерений) Экспорт

	Если Контейнер = Неопределено Тогда		
		Возврат;  		
	КонецЕсли; 
	
	ТекущаяНастройка  = НастройкиИзмерений[ИмяПоля];
	
	Если ТекущаяНастройка <> Неопределено Тогда
		
		СтандартнаяОбработка = Ложь;
		Если ТекущаяНастройка.ЭтоСоставнойТип Тогда
			Контейнер[ИмяПоля]   = Неопределено;
			// Добавление кода. Начало. 17.08.2015{{
			ЭлементВладелец.ОграничениеТипа = НастройкиИзмерений.Получить(ИмяПоля).ТипЗначения;
			// Добавление кода. Конец. 17.08.2015}}
		Иначе	
			Контейнер[ИмяПоля]   = ТекущаяНастройка.ЗначениеПоУмолчанию;
		КонецЕсли; 
		
	КонецЕсли; 
	
	ЭлементВладелец.ВыбиратьТип = Истина;

КонецПроцедуры // ОбработкаОчисткиДополнительногоИзмерения()

// Процедура инициализирует значения дополнительных измерений.
// 
// Параметры:
//  Контейнер   - СтрокаТабличнойЧасти,ДокументОбъект.
//  Измерения   - Структура.
//  НастройкиИзмерений - Соответствие.
// 
Процедура ИнициализироватьЗначенияДопИзмерений(Контейнер,Измерения,НастройкиИзмерений) Экспорт

	Если Контейнер = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли; 
	
	#Если ТолстыйКлиент Тогда
		Если Измерения = Неопределено Тогда
			
			Измерения = бит_Бюджетирование.ПолучитьИзмеренияБюджетирования("Дополнительные","Синоним");
			
		КонецЕсли; 
		
		Если НастройкиИзмерений = Неопределено Тогда
			
			НастройкиИзмерений = бит_ОбщиеПеременныеСервер.ЗначениеПеременной("бит_НастройкиДополнительныхИзмерений");
			
		КонецЕсли; 
	#КонецЕсли
	
	Для каждого КлючИЗначение Из Измерения Цикл
		
		ИмяИзмерения = КлючИЗначение.Ключ;
		
		ТекущаяНастройка = НастройкиИзмерений[ИмяИзмерения];
		
		Если ТекущаяНастройка <> Неопределено Тогда
			
			Если Контейнер[ИмяИзмерения] = Неопределено Тогда
            	Контейнер[ИмяИзмерения] = ТекущаяНастройка.ЗначениеПоУмолчанию;
			КонецЕсли;
		 			
		КонецЕсли;  		
		
	КонецЦикла;    	

КонецПроцедуры // ИнициализироватьЗначенияДопИзмерений()

#КонецОбласти

// Функция определяет является ли значение аналитики 
// документом бит_ПлатежнаяПозиция, бит_РасходнаяПозиция...
// 
// Параметры:
//  ТекЗначениеАналитики - Любое значение.
//  
// Возвращаемое значение:
//   Булево.
// 
Функция ЭтотДокументОбОплатеБит(ТекЗначениеАналитики) Экспорт

	Результат = бит_ОбщегоНазначенияКлиентСервер.ЭтоУказанныйТип(ТипЗнч(ТекЗначениеАналитики), "ДокументСсылка.бит_ПлатежнаяПозиция") 
			Или бит_ОбщегоНазначенияКлиентСервер.ЭтоУказанныйТип(ТипЗнч(ТекЗначениеАналитики), "ДокументСсылка.бит_РасходнаяПозиция")
			Или бит_ОбщегоНазначенияКлиентСервер.ЭтоУказанныйТип(ТипЗнч(ТекЗначениеАналитики), "ДокументСсылка.бит_ПланируемоеПоступлениеДенежныхСредств");

	Возврат Результат;
	
КонецФункции // ЭтотДокументОбОплатеБит()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура открывает форму дополнительных значений аналитики с отбором.
// 
// Параметры:
//  Контейнер			 - ДанныеФормыКоллекция, ДанныеФормыЭлементКоллекции, Соответствие.
//  ИмяАналитики         - Строка.
//  ИмяЭлемента          - Строка.
//  НастройкиИзмерений   - Соответствие.
//  ЭлементВладелец  	 - ЭлементУправления.
// 
Процедура ОткрытьФормуДопЗначенийАналитик(Контейнер, ИмяАналитики, ИмяЭлемента, НастройкиИзмерений, ЭлементВладелец)

	ТипКонтейнераСоответствие = ТипЗнч(Контейнер) = Тип("Соответствие");
	
	ТекущееЗначение = ?(ТипКонтейнераСоответствие, Контейнер.Получить(ИмяЭлемента), Контейнер[ИмяЭлемента]);
	
	Если ТипЗнч(ТекущееЗначение) = Тип("СправочникСсылка.бит_ДополнительныеЗначенияАналитик") Тогда
		
		ТекущаяНастройка = НастройкиИзмерений[ИмяАналитики];
		Если ТекущаяНастройка <> Неопределено Тогда
			
			НовыйПараметр = Новый ПараметрВыбора("Отбор.Владелец", ТекущаяНастройка.Аналитика);
			
			МассивПараметров = Новый Массив;
			МассивПараметров.Добавить(НовыйПараметр);
			
			ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
			
			ЭлементВладелец.ПараметрыВыбора = ПараметрыВыбора;
			
		КонецЕсли; 	
		
	КонецЕсли;	

КонецПроцедуры // ОткрытьФормуДопЗначенийАналитик()

#КонецОбласти
