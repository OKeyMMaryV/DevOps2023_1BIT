
#Область СлужебныйПрограммныйИнтерфейс

// Процедура вызывает форму выбора регистра и заполняет реквизит "Регистр".
// 
// Параметры:
//  ШаблонДвиженийОбъект  - СправочникОбъект.бит_ШаблоныДвижений.
// 
Процедура ЗаполнитьРегистрДляШаблонаДвижений(ШаблонДвиженийОбъект, ЭтаФорма) Экспорт
	
	#Если Клиент Тогда
		Если Не ЗначениеЗаполнено(ШаблонДвиженийОбъект.Регистр) Тогда
			
			ДопПараметры = Новый Структура("ЭтаФорма, ШаблонДвиженийОбъект", ЭтаФорма, ШаблонДвиженийОбъект);
			ОбработчикВыбора = Новый ОписаниеОповещения("ВыбратьВидРеквизитаДвижений_Завершение"
						, бит_МеханизмХозяйственныхОперацийКлиент.ЭтотОбъект, ДопПараметры);
			ОткрытьФорму("Справочник.бит_ВидыРеквизитовДвижений.ФормаВыбораГруппы", , , , , , ОбработчикВыбора, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
									
		КонецЕсли;	
	#КонецЕсли
	                                                     
КонецПроцедуры // ЗаполнитьРегистрДляШаблонаДвижений()

// Процедура заполняет реквизит "Регистр".
// 
// Параметры:
//  ШаблонДвиженийОбъект  - СправочникОбъект.бит_ШаблоныДвижений.
// 
Процедура ВыбратьВидРеквизитаДвижений_Завершение(ВыбранныйРегистр, ДопПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ВыбранныйРегистр) Тогда
			
		ДопПараметры.ШаблонДвиженийОбъект.Регистр = ВыбранныйРегистр;
		ПолучитьНаименованиеШаблонаДвижений(ДопПараметры.ШаблонДвиженийОбъект);
		ДопПараметры.ЭтаФорма.СоздатьКолонкиИЗаполнитьДвиженияНаСервере();// ВыбратьВидРеквизитаДвижений_Завершение();
		
	Иначе
		
		ДопПараметры.ЭтаФорма.Закрыть();
		ТекстСообщения = НСтр("ru='Не выбран регистр, для которого оформляется шаблон!'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
		
	КонецЕсли;	
	
КонецПроцедуры // ВыбратьВидРеквизитаДвижений_Завершение()

// Генерирует наименование для шаблона движений.
// 
// Параметры:
//  ШаблонОбъект  - СправочникОбъект.бит_ШаблоныДвижений.
// 
Процедура ПолучитьНаименованиеШаблонаДвижений(ШаблонОбъект) Экспорт

	ШаблонНаименование = "" + Строка(ШаблонОбъект.Регистр) + " --> " + Строка(ШаблонОбъект.ВидДокумента) + " --> " + ШаблонОбъект.Код;
	
	Если Найти(ШаблонОбъект.Наименование, " --> ") > 0 или СокрЛП(ШаблонОбъект.Наименование) = "" Тогда
		ШаблонОбъект.Наименование = ШаблонНаименование;
	КонецЕсли;
	
КонецПроцедуры // ПолучитьНаименованиеШаблонаДвижений()

// Добавляет шаблон в выбранный интерактивно комплект.
// 
// Параметры:
//  ШаблонОбъект  - СправочникОбъект.бит_ШаблоныДвижений.
//                 
Процедура ВключитьШаблонВКомплектИнтерактивно(ШаблонСсылка, ВидДокумента) Экспорт

	// Откроем модально форму выбора комплектов движений
	// с отбором по виду документа. Если вид документа - не заполнен
	// выведем сообщение об ошибке.
	
	Если не ЗначениеЗаполнено(ВидДокумента) Тогда
		
		ТекстСообщения = НСтр("ru='Не выбран вид документа!'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
		
		Возврат;
		
	КонецЕсли;
	
	УсловияОтбора = Новый Структура;
	УсловияОтбора.Вставить("ВидДокумента", ВидДокумента);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", УсловияОтбора);
	ФормаВыбора = ПолучитьФорму("Справочник.бит_КомплектыДвижений.ФормаВыбора", ПараметрыФормы);
	
	ДопПараметры = Новый Структура("ШаблонСсылка", ШаблонСсылка);
	ОбработчикВК = Новый ОписаниеОповещения("ВключитьШаблонВКомплект_Завершение", бит_МеханизмХозяйственныхОперацийКлиент.ЭтотОбъект, ДопПараметры);
	ОткрытьФорму(ФормаВыбора.ИмяФормы, , , , , , ОбработчикВК, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс); 
	
КонецПроцедуры // ВключитьШаблонВКомплектИнтерактивно()

// Процедура включает шаблон в комплект.
// 
Процедура ВключитьШаблонВКомплект_Завершение(ВыбранныйКомплект, ДопПараметры) Экспорт

	Если ЗначениеЗаполнено(ВыбранныйКомплект) Тогда
		бит_МеханизмХозяйственныхОперацийСервер.ВключитьШаблонВКомплект(ДопПараметры.ШаблонСсылка, ВыбранныйКомплект);
	КонецЕсли;

КонецПроцедуры // ВключитьШаблонВКомплект_Завершение()

#КонецОбласти
