/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Общий модуль выполняет обработку события "ОбработкаПроверкиЗаполнения" 
// Для объектов включенных в подписку "ок_ОбработкаПроверкиЗаполнения"
//
// Для расширения функциональности, необходимо:
// 1. Включить объект в подписку "ок_ОбработкаПроверкиЗаполнения"
// 2. В процедуре "ок_ОбработкаПроверкиЗаполненияОбработкаПроверкиЗаполнения" добавить ветку условия с проверкой на тип источника
// 3. Создать область по шаблону "ТипМетанных_ИмяОбъектаМетаданных"
// 4. Создать процедуру внутри области по шаблону "ОбработкаПроверкиЗаполненияТипаМетанных_ИмяОбъектаМетаданных"
// 5. Вызвать созданную процедуру из ветки условия в процедуре "ок_ОбработкаПроверкиЗаполненияОбработкаПроверкиЗаполнения"
// 6. Необходимые служебные процедуры и функции размещать в области модуля "СлужебныеПроцедурыИФункции"
/////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

//Процедура вызываемая подпиской на событие "ок_ОбработкаПроверкиЗаполнения"
// Параметры:
// Источник - Справочник, Документ, ПВХ, Тип - МетаданныеОбъект
// Отказ - Флаг отказа, Тип - Булево
// ПроверяемыеРеквизиты - Список проверяемых реквизитов, Тип - Массив
Процедура ок_ОбработкаПроверкиЗаполненияОбработкаПроверкиЗаполнения(Источник, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	ТипИсточника = ТипЗнч(Источник);
	
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.Проекты") Тогда
		ОбработкаПроверкиЗаполненияСправочника_Проекты(Источник, Отказ, ПроверяемыеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Справочник_Проекты

Процедура ОбработкаПроверкиЗаполненияСправочника_Проекты(Источник, Отказ, ПроверяемыеРеквизиты)
	
	ВыполнитьПроверкуРеквизитовПроектаПоУмолчанию(Источник, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьПроверкуРеквизитовПроектаПоУмолчанию(Источник, Отказ, ПроверяемыеРеквизиты)
	
	Если Не Источник.ок_ПроектПоУмолчанию Тогда 
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ок_Период"));
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Проекты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Проекты КАК Проекты
			|ГДЕ
			|	Проекты.ок_ПроектПоУмолчанию
			|	И Проекты.ок_Период = &ок_Период
			|	И Проекты.Ссылка <> &Ссылка";
		
		Запрос.УстановитьПараметр("ок_Период", НачалоГода(Источник.ок_Период));
		Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда 
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			ВыборкаДетальныеЗаписи.Следующий();
			ТекстСообщения = СтрШаблон(НСтр("ru = 'За %1 год уже существует проект по умолчанию %2'"),Формат(Источник.ок_Период,"ДФ=гггг"),ВыборкаДетальныеЗаписи.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Источник, ,	"Объект", Отказ);
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
