////////////////////////////////////////////////////////////////////////////////
// Модуль содержит процедуры/функции подсистемы Бюджетирования доступные на клиенте.
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Функция - Пользовательское представление формулы
//
// Параметры:
//  ТабДокДанные - 	ТабличныйДокумент - данные таблицы.
//  Формула		 - 	Строка - формула.
// 
// Возвращаемое значение:
//  ФормулаПредставление - Строка.
//
Функция ПользовательскоеПредставлениеФормулы(ТабДокДанные, Формула) Экспорт
	
	ФормулаПредставление = Формула;
	
	МассивАргументы = бит_СтрокиКлиентСервер.РазобратьФормулу(Формула);
	Для Каждого Арг ИЗ МассивАргументы Цикл
		
		ОблАрг = ТабДокДанные.Области.Найти(Арг);
		Если бит_БюджетированиеКлиентСервер.ЭтоПрямоугольнаяОбласть(ОблАрг) Тогда
			
			ИмяАльт = бит_БюджетированиеКлиентСервер.ПользовательскоеПредставлениеОбласти(ОблАрг);
			ФормулаПредставление = СтрЗаменить(ФормулаПредставление, ОблАрг.Имя, ИмяАльт);
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат ФормулаПредставление;
	
КонецФункции

// Процедура выделяет аргументы формулы, на которой установлен курсор. 
// 
// Параметры:
//  ТабДокДанные - ТабличныйДокумент
//  Формула - Строка.
//  ВыделенныеОбласти - СписокЗначений.
// 
Процедура ВыделитьАргументыФормулы(ТабДокДанные, Формула, ВыделенныеОбласти, вхЦветАргументов = Неопределено) Экспорт
	
	Если вхЦветАргументов = Неопределено Тогда
		ЦветАргументов = Новый Цвет(241,241,241);		
	Иначе	
		ЦветАргументов = вхЦветАргументов;		
	КонецЕсли; 
	
	МассивАргументы = бит_СтрокиКлиентСервер.РазобратьФормулу(Формула);
	Для Каждого Арг ИЗ МассивАргументы Цикл
		ОблАрг = ТабДокДанные.Области.Найти(Арг);
		Если бит_БюджетированиеКлиентСервер.ЭтоПрямоугольнаяОбласть(ОблАрг) Тогда
			ЛинияВыделения = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,2,0);
			#Если НЕ ВебКлиент Тогда
				ОблАрг.Обвести(ЛинияВыделения,ЛинияВыделения,ЛинияВыделения,ЛинияВыделения);
			#КонецЕсли
			ОблАрг.Узор = ТипУзораТабличногоДокумента.Узор8;
			ОблАрг.ЦветУзора = ЦветАргументов;
			ОблАрг.ЦветРамки = WebЦвета.Черный;
			ВыделенныеОбласти.Добавить(Арг);
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

// Процедура заполняет проценты равномерно по профилю. 
//
Процедура РавномерныйПрофиль(ТаблицаПрофиль) Экспорт

	Если ТаблицаПрофиль.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;
	
	Коэффициенты = Новый Массив; 
	Для каждого СтрокаТаблицы Из ТаблицаПрофиль Цикл
		Коэффициенты.Добавить(1);
	КонецЦикла; 
	
	Коэффициенты = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(
						100, Коэффициенты, 3);
	Индекс = 0;				
	Для каждого СтрокаТаблицы Из ТаблицаПрофиль Цикл
		СтрокаТаблицы.Процент = Коэффициенты[Индекс];
		Индекс = Индекс + 1;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВеделитьОбласть(ДокументРезультат, Области) Экспорт

	Для каждого Элемент Из Области Цикл
		Область = ДокументРезультат.Области.Найти(Элемент.Значение);
		Если бит_БюджетированиеКлиентСервер.ЭтоПрямоугольнаяОбласть(Область)  Тогда
			ЛинияВыделения = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,3,0);
			#Если НЕ ВебКлиент Тогда
				Область.Обвести(ЛинияВыделения,ЛинияВыделения,ЛинияВыделения,ЛинияВыделения);
			#КонецЕсли
			Область.ЦветРамки = Новый Цвет(251, 196, 0);
		КонецЕсли; 
	КонецЦикла; 	

КонецПроцедуры

Функция ИмяВторойЯчейки(вхИмя) Экспорт
	
	Префикс   = бит_БюджетированиеКлиентСервер.ПрефиксВторогоРесурса(Лев(вхИмя, 1));
	Имя = Префикс+Сред(вхИмя,2);
	
	Возврат Имя;
	
КонецФункции

Функция ПолучитьФормулуИзПредставления(ТабДокДанные, вхПредставлениеФормулы) Экспорт
	
	ПредставлениеФормулы = СтрЗаменить(вхПредставлениеФормулы, "[[", "[");
	ПредставлениеФормулы = СтрЗаменить(ПредставлениеФормулы, "]]", "]");	
	
	Формула = ПредставлениеФормулы;
	
	МассивАргументы = бит_СтрокиКлиентСервер.РазобратьФормулу(ПредставлениеФормулы);
	Для Каждого Арг ИЗ МассивАргументы Цикл
		
		Если НЕ ЗначениеЗаполнено(Арг) Тогда
		
			Продолжить;
		
		КонецЕсли; 
		
		ИмяОбл = ТабДокДанные.Область(Арг).Имя;
		Формула = СтрЗаменить(Формула, Арг, ИмяОбл);
				
	КонецЦикла;	
	
	Возврат Формула;
	
КонецФункции

#КонецОбласти 