
#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьПредставлениеРНПТ(Форма, СтрокаТабличнойЧасти, ЕстьПодсказкаРНПТ = Истина) Экспорт
	
	Объект = Форма.Объект;
	ДокументСсылка = Объект.Ссылка;
	ЭтоВвозИзЕАЭС  = (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") 
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаПоступления"))
		И Форма.ПоставщикРезидентТаможенногоСоюза;
	ЭтоВводОстатков = ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВводНачальныхОстатков");
	
	Если СтрокаТабличнойЧасти.ПрослеживаемыйТовар
		И НЕ ЭтоВвозИзЕАЭС Тогда
		Если ЕстьПодсказкаРНПТ Тогда 
			СтрокаТабличнойЧасти.ПодсказкаРНПТ = "РНПТ:";
		КонецЕсли;
		Если ЭтоВводОстатков Тогда
			СтрокиСРНПТ = Объект.СведенияПрослеживаемости.НайтиСтроки(Новый Структура("Ключ", СтрокаТабличнойЧасти.Ключ));
		Иначе
			СтрокиСРНПТ = Объект.СведенияПрослеживаемости.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаТабличнойЧасти.ИдентификаторСтроки));
		КонецЕсли;
		КоличествоРНПТ = СтрокиСРНПТ.Количество();
		Если КоличествоРНПТ = 0 Тогда
			СтрокаТабличнойЧасти.РНПТ = "";
		ИначеЕсли КоличествоРНПТ = 1 Тогда
			СтрокаТабличнойЧасти.РНПТ = СокрЛП(Строка(СтрокиСРНПТ[0].РНПТ));
		Иначе
			СтрокаТабличнойЧасти.РНПТ = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 и еще %2'"),
			СокрЛП(Строка(СтрокиСРНПТ[0].РНПТ)),
			КоличествоРНПТ-1);
		КонецЕсли;
	ИначеЕсли ЕстьПодсказкаРНПТ Тогда
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтранаПроисхождения) Тогда
			СтрокаТабличнойЧасти.ПодсказкаРНПТ = "ТД:";
		Иначе
			СтрокаТабличнойЧасти.ПодсказкаРНПТ = "";
		КонецЕсли;
		СтрокаТабличнойЧасти.РНПТ = Строка(СтрокаТабличнойЧасти.НомерГТД);
	КонецЕсли;

КонецПроцедуры

//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-10-21 (#4405)
Процедура ЗаполнитьПредставлениеРНПТОС(Форма, СтрокаТабличнойЧасти) Экспорт
	Объект = Форма.Объект;
	ДокументСсылка = Объект.Ссылка;

	РНПТ = "";
	СтрокиСРНПТ = Объект.СведенияПрослеживаемости.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаТабличнойЧасти.ИдентификаторСтроки));
	
	КоличествоРНПТ = СтрокиСРНПТ.Количество();
	Если КоличествоРНПТ = 0 Тогда
		СтрокаТабличнойЧасти.РНПТ = "";
	ИначеЕсли КоличествоРНПТ = 1 Тогда
		СтрокаТабличнойЧасти.РНПТ = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 - %2'"),
		СокрЛП(Строка(СтрокиСРНПТ[0].СтранаПроисхождения)),
		СокрЛП(Строка(СтрокиСРНПТ[0].РНПТ)));
	Иначе
		СтрокаТабличнойЧасти.РНПТ = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 - %2 и еще %3'"),
		СокрЛП(Строка(СтрокиСРНПТ[0].СтранаПроисхождения)),
		СокрЛП(Строка(СтрокиСРНПТ[0].РНПТ)),
		КоличествоРНПТ-1);
	КонецЕсли;
	
КонецПроцедуры
//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-10-21 (#4405)
#КонецОбласти